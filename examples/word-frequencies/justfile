# This `justfile` should allow you to run a debug build of `falconeri` on a
# local k8s cluster. See the guide for instructions on how to set up a local
# cluster (on Mac or Linux), and how to point your `kubectl` at it.
#
# You can find the input data in `texts`.

# Build our image.
image:
    docker build -t word-frequencies .

# Set up a proxy.
proxy:
    cargo run -p falconeri -- proxy


# Set up MinIO CLI alias (requires proxy running).
mc-alias:
    #!/usr/bin/env bash
    set -euo pipefail
    MINIO_PASS=$(kubectl get secret falconeri-minio -o jsonpath='{.data.MINIO_ROOT_PASSWORD}' | base64 -d)
    mc alias set local http://localhost:9000 minioadmin "$MINIO_PASS"

# Upload test texts to MinIO (requires proxy running and mc-alias).
upload:
    mc mb --ignore-existing local/falconeri-test
    mc cp texts/*.txt local/falconeri-test/texts/

# Run our example job.
run:
    cargo run -p falconeri -- job run word-frequencies.s3.json

# Run job and wait for completion.
run-wait:
    #!/usr/bin/env bash
    set -euo pipefail
    JOB_ID=$(just run)
    echo "Job ID: $JOB_ID"
    cargo run -p falconeri -- job wait "$JOB_ID"

# Print results from MinIO (requires proxy running and mc-alias).
# Output filename mirrors input: texts/foo.txt -> word-frequencies/foo.txt
results:
    mc cat local/falconeri-test/word-frequencies/buffalo-public-library-1883.txt | head -20

# Delete results from MinIO (to verify fresh output on re-runs).
delete-results:
    mc rm --recursive --force local/falconeri-test/word-frequencies/

# Delete input texts from MinIO.
delete-inputs:
    mc rm --recursive --force local/falconeri-test/texts/

# Full cleanup: delete all test data from MinIO.
cleanup: delete-results delete-inputs

# Full test: Upload, run, wait, check results.
test: upload delete-results run-wait
    just results
